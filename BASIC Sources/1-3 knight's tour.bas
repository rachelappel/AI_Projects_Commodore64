10 ::::::::::::::::::::::::::::::::::::
20 REM KNIGHT'S TOUR
30 REM WRITTEN BY TIMOTHY J. O'MALLEY
40 REM COPYRIGHT 1983, TAB BOOKS INC.
50 REM (WRITTEN FOR THE COMMODORE 64)
60 ::::::::::::::::::::::::::::::::::::
70 REM *** MAIN PROGRAM ***
80 GOSUB 200:REM PRINT INSTRUCTIONS
90 GOSUB 400:REM DEFINE GRAPHICS
100 GOSUB 600:REM DISPLAY SOLUTION
110 OPEN 1,0:INPUT#1,N$:CLOSE 1:PRINTCHR$(147):POKE53281,6:POKES,0:POKES+1,0
115 END
120 :::::::::::::::::::::::::::::::::::
200 REM *** INSTRUCTIONS ***
210 PRINTCHR$(147);:REM CLEAR SCREEN
220 PRINT:PRINTTAB(13);"KNIGHT'S TOUR":PRINT
230 PRINT"-    THIS PROGRAM DISPLAYS A KNIGHT'S"
240 PRINT"  TOUR, THAT IS, A KNIGHT IN THE GAME"
250 PRINT"  OF CHESS CAN JUMP ON ALL 64 SQUARES"
260 PRINT"  OF A CHESS BOARD WITHOUT LANDING ON"
270 PRINT"  ANY SQUARE MORE THAN ONCE."
280 PRINT
290 OPEN 1,0
300 PRINT"  ENTER KNIGHT'S STARTING POSITION.":PRINT
310 PRINT"  ENTER THE ROW: ";:INPUT#1,R:PRINT
320 PRINT"  ENTER THE COLUMN: ";:INPUT#1,C:PRINT
330 CLOSE 1
340 IFR<1ORR>8ORC<1ORC>8THEN290
350 RETURN
360 :::::::::::::::::::::::::::::::::::
400 REM *** DEFINE GRAPHICS ***
410 PRINTCHR$(147);:REM CLEAR SCREEN
420 FORI=0TO63:READJ:POKE832+I,J:NEXTI
430 DATA,,,,,,,,,,,
440 DATA,8,,,30,,,63,,,127,
450 DATA,111,,,15,,,31,,,62,
460 DATA,62,,,28,,,62,,,127,
470 DATA,127,,,,,,,,,,,,,,
480 S=53248
490 POKES+21,1:POKES+39,1
500 POKES+23,1:POKES+29,1:POKE2040,13:POKE53281,14
510 P=1016:L=-1:FORJ=1TO8:FORM=1TO3
520 P=P+7:FORK=1TO8
530 FORN=1TO4:P=P+1:POKEP,160:POKEP+54272,ABS(L)*3
540 NEXTN:L=NOTL:NEXTK:P=P+1:NEXTM
550 L=NOTL:NEXTJ:RETURN
560 :::::::::::::::::::::::::::::::::::
600 REM *** DISPLAY SOLUTION ***
601 DATA3,42,5,20,37,40,15,18
602 DATA6,21,2,41,16,19,36,39
603 DATA43,4,57,54,59,38,17,14
604 DATA22,7,62,1,56,53,60,35
605 DATA49,44,55,58,61,64,13,28
606 DATA8,23,48,63,52,29,34,31
607 DATA45,50,25,10,47,32,27,12
608 DATA24,9,46,51,26,11,30,33
610 DIM BD(8,8),MD(8,8)
615 FORJ=1TO8:FORK=1TO8:READMD(J,K):NEXTK,J
620 N=0:X1=0:Y1=0
625 FC=MD(R,C)-1
626 FORJ=1TO8:FORK=1TO8:BD(J,K)=MD(J,K)-FC:IFBD(J,K)<1THENBD(J,K)=BD(J,K)+64
627 NEXTK,J
630 N=N+1:FORJ=1TO8:FORK=1TO8:IFBD(J,K)=NTHENR=J:C=K
633 NEXTK,J
635 P=1065+4*(C-1)+120*(R-1)
640 V=48+INT(N/10):POKEP,V
650 V=48+N-10*INT(N/10):POKEP+1,V
660 X2=32*C-18:Y2=24*R+16:DX=X2-X1:DY=Y2-Y1
670 IFABS(DY)>ABS(DX)THEN850
680 FORJ=SGN(DX)TODXSTEPSGN(DX)
690 X2=X1+J:Y2=Y1+J*DY/DX
700 POKES,X2:POKES+1,Y2
710 NEXTJ:IFN=64THENRETURN
750 X1=X2:Y1=Y2:GOTO630
850 FORJ=SGN(DY)TODYSTEPSGN(DY):Y2=Y1+J:X2=X1+J*DX/DY:GOTO700
860 :::::::::::::::::::::::::::::::::::