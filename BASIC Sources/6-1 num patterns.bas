10 ::::::::::::::::::::::::::::::::::::
20 REM *** PATTERNS IN NUMBERS ***
30 REM WRITTEN BY TIMOTHY J. O'MALLEY
40 REM COPYRIGHT 1984, TAB BOOKS INC.
50 REM (WRITTEN FOR THE COMMODORE 64)
60 ::::::::::::::::::::::::::::::::::::
70 DIM A(6,6),B$(100),C$(100):CT=0
75 PRINT"{CLR}";TAB(9)"*** NUMBER GUESSER ***":PRINT
80 PRINT"     ENTER FIVE NUMBERS, ONE AT A TIME."
90 PRINT"I WILL TRY TO PREDICT THE SIXTH NUMBER"
100 PRINT"IN THE SERIES.":PRINT
110 FOR J=1 TO 5:INPUT A(1,J):NEXT
120 FOR I=2 TO 5:FOR J=1 TO 6-I
130 A(I,J)=A(I-1,J+1)-A(I-1,J)
140 NEXT J,I
150 A(6,1)=A(5,1)
160 FOR I=5 TO 1 STEP -1
170 A(I,7-I)=A(I,6-I)+A(I+1,6-I)
180 NEXT
190 H=A(1,6):PRINT:PRINT "IS IT"STR$(H);
200 INPUT A$
210 IF LEFT$(A$+"Y",1)="Y" THEN PRINT "GOOD!":PRINT:GOSUB 340:GOTO 75
220 N$="":FOR N=1 TO 5:N$=N$+STR$(A(1,N)):NEXT
230 IF CT=0 THEN 300
240 FOR I=1 TO CT
250 Y$=B$(I):IF N$<>Y$ THEN 290
260 X$=C$(I):PRINT "IS IT "X$;
270 INPUT A$
280 IF LEFT$(A$+"Y",1)="Y" THEN PRINT "GREAT!":PRINT:GOSUB 340:GOTO 75
290 NEXT
300 INPUT "WHAT IS THE ANSWER";Z$
310 CT=CT+1:B$(CT)=N$:C$(CT)=Z$
320 PRINT "I'LL REMEMBER THAT!":PRINT:GOSUB 340:GOTO 75
330 :::::::::::::::::::::::::::::::::::
340 FOR I=1 TO 5000:NEXT:RETURN
350 :::::::::::::::::::::::::::::::::::