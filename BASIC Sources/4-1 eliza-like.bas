10 ::::::::::::::::::::::::::::::::::::
20 REM AN "ELIZA"-LIKE PROGRAM
30 REM WRITTEN BY TIMOTHY J. O'MALLEY
40 REM COPYRIGHT 1984, TAB BOOKS INC.
50 REM (WRITTEN FOR THE COMMODORE 64)
60 ::::::::::::::::::::::::::::::::::::
70 REM *** MAIN PROGRAM ***
80 GOSUB 170:REM SET UP PROGRAM
90 GOSUB 250:REM INPUT SUBROUTINE
100 GOSUB 390:REM SYNONYMS AND ANTONYMS
110 GOSUB 570:REM LOOK FOR KEYWORDS
120 GOSUB 1120:REM REMOVE MARKERS
130 GOSUB 1190:REM COMPUTER'S REPLY
140 GOTO 90:REM ENDLESS LOOP
150 :::::::::::::::::::::::::::::::::::
160 REM *** SET UP PROGRAM ***
170 M=50:REM NUMBER OF SYNONYMS & ANTONYMS
180 U=84:REM NUMBER OF POSSIBLE REPLIES
190 DIM MY$(200):MC=0:REM STACK OF "MY" STATEMENTS
200 PRINT "{CLR}";:PRINT "{WHITE}";:REM CLEAR SCREEN AND SET COMPUTER'S COLOR AS WHITE
210 PRINT "     HELLO. WHAT'S ON YOUR MIND?":PRINT
220 RETURN
230 :::::::::::::::::::::::::::::::::::
240 REM *** INPUT SUBROUTINE ***
250 RESTORE:REM ALLOW DATA TO BE RE-READ
260 X=0:A$="":PRINT "{CYAN}";:REM KEYWORD FLAG=0 & YOUR INPUT COLOR IS CYAN
270 OPEN 50,0:REM OPEN THE KEYBOARD FILE TO "READ" KEYBOARD
280 GET#50,Z$:IF Z$="" THEN 280:REM RE-READ KEYBOARD
290 IF Z$=CHR$(13) THEN PRINT:CLOSE50:PRINT "{WHITE}";:GOTO 320:REM END INPUT
300 IF Z$=CHR$(20) THEN PRINT Z$;:A$=LEFT$(A$,LEN(A$)-1):GOTO 280:REM DEL CHAR
310 A$=A$+Z$:PRINT Z$;:GOTO 280:REM CONCATENATE INPUT STRING & DISPLAY CHAR
320 IF A$="" THEN PRINT "WHAT'S THE PROBLEM?":X=1:RETURN:REM NO ENTRY
330 IF A$=R$ THEN PRINT "YOU ARE BEING REPETITIOUS.":X=1:RETURN
340 R$=A$:REM STORE LAST INPUT
350 A$=" "+LEFT$(A$,LEN(A$)-1)+" ":REM ERASE PUNCTUATION AND ADD SPACES
360 RETURN
370 :::::::::::::::::::::::::::::::::::
380 REM *** SYNONYMS AND ANTONYMS ***
390 IF X THEN RETURN:REM NO VALID INPUT
400 FOR I=1 TO M/2
410 READ E$,N$:REM READ WORD PAIR
420 FOR S=1 TO LEN(A$)-LEN(E$)+1
430 IF MID$(A$,S,1)<>" " THEN 530:REM MIDDLE OF WORD
440 IF E$=MID$(A$,S,LEN(E$)) THEN A$=LEFT$(A$,S-1)+N$+MID$(A$,S+LEN(E$))
450 DATA " MOM "," MOTHER "," DAD "," FATHER "," KIDS "," CHILDREN "
460 DATA " DREAMS "," DREAM "," KID "," CHILD "," HOUSE "," HOME* "
470 DATA " I "," YOU@ "," YOU "," I "," ME "," YOU "," ONE "," 1 "
480 DATA " MY "," YOUR* "," TWO "," 2 "," THREE "," 3 "
490 DATA " YOUR "," MY "," MYSELF "," YOURSELF* "," TOO "," ALSO "
500 DATA " YOURSELF "," MYSELF "," HURT "," HARM "," HOME "," HOUSE* "
510 DATA " I'M "," YOU'RE* "," YOU'RE "," I'M "," AM "," ARE@ "
520 DATA " WERE "," WAS "," EASY "," SIMPLE "," DIFFICULT "," HARD "
530 NEXT S,I
540 RETURN
550 :::::::::::::::::::::::::::::::::::
560 REM *** LOOK FOR KEYWORDS ***
570 IF X THEN RETURN
580 FOR I=1 TO U
590 READ E$,J
600 FOR S=1 TO LEN(A$)-LEN(E$)+1
620 IF E$=MID$(A$,S,LEN(E$)) THEN R$=MID$(A$,S+LEN(E$)):GOTO 1910
630 NEXT S,I
640 GOTO 950:REM NO KEYWORDS LOCATED
650 R$=LEFT$(R$,LEN(R$)-1):RETURN
660 DATA "COMPUTER",1,"MACHINE",1
670 DATA " NAME ",2,"ALIKE",3," LIKE ",3," SAME ",3
680 DATA "YOU@ REMEMBER",4,"DO I REMEMBER",5,"YOU@ DREAMED",6
690 DATA " DREAM ",7," IF ",8,"EVERYBODY",9,"EVERYONE",9
700 DATA "NOBODY",9,"NO ONE",9,"WAS YOU@",10,"YOU@ WAS",11
710 DATA "WAS I",12,"YOUR* MOTHER",13,"YOUR* FATHER",13
720 DATA "YOUR* HUSBAND",13,"YOUR* CHILDREN",13,"YOUR*",14
730 DATA "YOUR* SISTER",13,"YOUR* BROTHER",13,"YOUR* WIFE",13
740 DATA "ALWAYS",15,"ARE I",16,"ARE@ YOU@",18," HOW ",25
750 DATA "BECAUSE",19,"CAN I",20,"CAN YOU@",21,"CERTAINLY",22
760 DATA "DEUTSCH",23,"ESPANOL",23,"FRANCAIS",23,"HELLO",24
770 DATA "I REMIND YOU OF",3,"I ARE",26,"I'M",26
780 DATA "ITALIANO",23,"MAYBE",28," MY ",29," NO ",30
790 DATA "PERHAPS",28,"SORRY",31,"WHAT ",25,"WHEN ",25
800 DATA "WHY DON'T I",32,"WHY CAN'T YOU@",33,"YES",22
810 DATA "YOU@ WANT",34,"YOU@ NEED",34," ARE ",17," I ",27
820 DATA "YOU@ ARE@ SAD",35,"YOU'RE* SAD",35
830 DATA "YOU@ ARE@ UNHAPPY",35,"YOU'RE* UNHAPPY",35
840 DATA "YOU@ ARE@ DEPRESSED",35,"YOU'RE* DEPRESSED",35
850 DATA "YOU@ ARE@ SICK",35,"YOU'RE* SICK",35
860 DATA "YOU@ ARE@ HAPPY",36,"YOU'RE* HAPPY",36
870 DATA "YOU@ ARE@ ELATED",36,"YOU'RE* ELATED",36
880 DATA "YOU@ ARE@ GLAD",36,"YOU'RE* GLAD",36
890 DATA "YOU@ ARE@ BETTER",36,"YOU'RE* BETTER",36
900 DATA "YOU@ FEEL YOU@",37,"YOU@ THINK YOU@",37
910 DATA "YOU@ BELIEVE YOU@",37,"YOU@ WISH YOU@",37
920 DATA " YOU@ ARE@",38,"YOU'RE*",38,"YOU@ CAN'T",39
930 DATA "YOU@ CANNOT",39,"YOU@ DON'T",40,"YOU@ FEEL",41
940 DATA " HE ",42," SHE ",43
945 :::::::::::::::::::::::::::::::::::
950 REM *** NO KEYWORDS ***
960 X=1:IF MC=0 THEN 980
970 RAN=1+INT(5*RND(1)):ON RAN GOTO 980,1030,1030,1030,980
980 RAN=1+INT(4*RND(1)):ON RAN GOTO 990,1000,1010,1020
990 PRINT"I AM NOT SURE I UNDERSTAND YOU FULLY.":RETURN
1000 PRINT"PLEASE GO ON.":RETURN
1010 PRINT"WHAT DOES THAT SUGGEST TO YOU?":RETURN
1020 PRINT"DO YOU FEEL STRONGLY ABOUT DISCUSSING SUCH THINGS?":RETURN
1030 Y$=MY$(1+INT(MC*RND(1))):RAN=1+INT(6*RND(1))
1040 ON RAN GOTO 1050,1060,1070,1080,1090,1100
1050 PRINT"LET'S DISCUSS FURTHER WHY YOUR"+Y$+".":RETURN
1060 PRINT"EARLIER YOU SAID YOUR"+Y$+".":RETURN
1070 PRINT"DOES THAT HAVE ANYTHING TO DO WITH THE FACT THAT YOUR"+Y$+".":RETURN
1080 PRINT"TELL ME MORE ABOUT WHY YOUR"+Y$+".":RETURN
1090 PRINT"IS IT REALLY TRUE THAT YOUR"+Y$+"?":RETURN
1100 PRINT"LET'S SEE. YOU TOLD ME YOUR"+Y$+". CARE TO ELABORATE?":RETURN
1105 ::::::::::::::::::::::::::::::::::
1110 REM *** REMOVE @ AND * MARKERS ***
1120 IF X=1 THEN RETURN
1130 FOR S=1 TO LEN(R$)
1140 G$=MID$(R$,S,1):IF G$="@" OR G$="*" THEN R$=LEFT$(R$,S-1)+MID$(R$,S+1)
1150 NEXT S
1160 RETURN
1170 ::::::::::::::::::::::::::::::::::
1180 REM *** COMPUTER'S REPLY ***
1190 IF X=1 THEN RETURN
1200 IF J<11 THEN ON J GOTO 1310,1320,1330,1340,1350,1360,1370,1380,1390,1400
1210 J=J-10
1220 IF J<11 THEN ON J GOTO 1410,1420,1430,1440,1450,1460,1470,1480,1490,1500
1230 J=J-10
1240 IF J<11 THEN ON J GOTO 1510,1520,1530,1540,1550,1560,1570,1580,1590,1600
1250 J=J-10
1260 IF J<11 THEN ON J GOTO 1610,1660,1670,1680,1690,1700,1710,1720,1730,1740
1270 J=J-10
1280 ON J GOTO 1750,2070,2080
1290 ::::::::::::::::::::::::::::::::::
1300 REM *** COMPUTER'S REPLIES ***
1310 PRINT"DO COMPUTERS WORRY YOU?":RETURN
1320 PRINT"I AM NOT INTERESTED IN NAMES.":RETURN
1330 PRINT"IN WHAT WAY?":RETURN
1340 PRINT"DO YOU OFTEN THINK OF"R$"?":RETURN
1350 PRINT"DID YOU REALLY THINK I WOULD FORGET"R$"?":RETURN
1360 PRINT"REALLY, "R$"?":RETURN
1370 PRINT"WHAT DOES THAT DREAM SUGGEST TO YOU?":RETURN
1380 PRINT"DO YOU THINK THAT IT'S LIKELY THAT IF"R$"?":RETURN
1390 PRINT"REALLY, "E$"?":RETURN
1400 PRINT"WHAT IF YOU WERE"R$"?":RETURN
1410 PRINT"WERE YOU REALLY?":RETURN
1420 PRINT"WOULD YOU LIKE TO BELIEVE I WAS"R$"?":RETURN
1430 PRINT"TELL ME MORE ABOUT YOUR FAMILY.":RETURN
1440 GOSUB 1960:GOSUB 1780:RETURN
1450 PRINT"CAN YOU THINK OF A SPECIFIC EXAMPLE?":RETURN
1460 PRINT"WHY ARE YOU INTERESTED IN WHETHER I AM"R$" OR NOT?":RETURN
1470 PRINT"DID YOU THINK THEY MIGHT NOT BE "R$"?":RETURN
1480 PRINT"DO YOU BELIEVE YOU ARE"R$"?":RETURN
1490 PRINT"IS THAT THE REAL REASON?":RETURN
1500 PRINT"YOU BELIEVE I CAN"R$", DON'T YOU?":RETURN
1510 PRINT"WHETHER YOU CAN"R$" DEPENDS ON YOU MORE THAN ON ME.":RETURN
1520 PRINT"YOU SEEM QUITE SURE.":RETURN
1530 PRINT"SORRY, I SPEAK ONLY ENGLISH.":RETURN
1540 PRINT"HOW DO YOU DO?":RETURN
1550 PRINT"WHY DO YOU ASK?":RETURN
1560 PRINT"WHAT MAKES YOU THINK I AM"R$"?":RETURN
1570 PRINT"LET'S NOT TALK ABOUT ME.":RETURN
1580 PRINT"YOU ARE CERTAIN?":RETURN
1590 PRINT"WHY ARE YOU CONCERNED OVER MY "R$"?":RETURN
1600 PRINT"ARE YOU SAYING 'NO' JUST TO BE NEGATIVE?":RETURN
1610 RAN=1+INT(4*RND(1)):ON RAN GOTO 1620,1630,1640,1650
1620 PRINT"PLEASE DON'T APOLOGIZE.":RETURN
1630 PRINT"APOLOGIES ARE NOT NECESSARY.":RETURN
1640 PRINT"WHAT FEELING DO YOU HAVE WHEN YOU APOLOGIZE?":RETURN
1650 PRINT"YOU NEEDN'T FEEL YOU HAVE TO APOLOGIZE.":RETURN
1660 PRINT"DO YOU BELIEVE I DON'T"R$"?":RETURN
1670 PRINT"DO YOU THINK YOU SHOULD BE ABLE TO"R$"?":RETURN
1680 PRINT"WHAT WOULD IT MEAN TO YOU IF YOU GOT"R$"?":RETURN
1690 GOSUB 1840:GOSUB 1880:PRINT"I'M SORRY TO HEAR YOU ARE"R$".":RETURN
1700 GOSUB 1840:GOSUB 1880:PRINT"HOW HAVE I HELPED YOU BE"R$"?":RETURN
1710 PRINT"DO YOU REALLY THINK SO?":RETURN
1720 PRINT"IS IT BECAUSE YOU ARE"R$" THAT YOU CAME TO ME?":RETURN
1730 PRINT"HOW DO YOU KNOW YOU CAN'T"R$"?":RETURN
1740 PRINT"DON'T YOU REALLY"R$"?":RETURN
1750 PRINT"TELL ME MORE ABOUT SUCH FEELINGS.":RETURN
1760 ::::::::::::::::::::::::::::::::::
1770 REM *** KEYWORD IS "MY" ***
1780 IF LEN(R$)<12 THEN RETURN
1790 MC=MC+1:ID=MC:IF MC>200 THEN MC=200:ID=1+INT(200*RND(1))
1800 MY$(ID)=R$
1810 RETURN
1820 ::::::::::::::::::::::::::::::::::
1830 REM *** REMOVE @ MARKER ***
1840 IF MID$(E$,4,1)="@" THEN R$=RIGHT$(E$,LEN(E$)-9)
1850 RETURN
1860 ::::::::::::::::::::::::::::::::::
1870 REM *** REMOVE * MARKER ***
1880 IF MID$(E$,7,1)="*" THEN R$=RIGHT$(E$,LEN(E$)-7)
1890 RETURN
1900 ::::::::::::::::::::::::::::::::::
1910 IF R$<>"" THEN 650
1920 RETURN
1950 ::::::::::::::::::::::::::::::::::
1960 RAN=1+INT(8*RND(1))
1970 ON RAN GOTO 1980,1990,2000,2010,2020,2030,2040
1980 PRINT"YOUR"R$"?":GOSUB 1780:RETURN
1990 PRINT"WHY IS IT THAT YOUR"R$"?":GOSUB 1780:RETURN
2000 PRINT"YOUR"R$"!":GOSUB 1780:RETURN
2010 PRINT"HOW IS IT THAT YOUR"R$"?":GOSUB 1780:RETURN
2020 PRINT"YOUR"R$"? REALLY?":GOSUB 1780:RETURN
2030 PRINT"MY"R$", TOO.":GOSUB 1780:RETURN
2040 PRINT"WHOSE"R$"?":GOSUB 1780:RETURN
2050 PRINT"YOU SAY YOUR"R$"?":GOSUB 1780:RETURN
2060 ::::::::::::::::::::::::::::::::::
2070 PRINT"DOES HE HAVE A PROBLEM?":RETURN
2080 PRINT"TELL ME MORE ABOUT HER.":RETURN
2090 ::::::::::::::::::::::::::::::::::