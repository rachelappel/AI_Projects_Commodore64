10 ::::::::::::::::::::::::::::::::::::
20 REM TRAINING THE RAT - PROGRAM 7
30 REM WRITTEN BY TIMOTHY J. O'MALLEY
40 REM COPYRIGHT 1984, TAB BOOKS INC.
50 REM (WRITTEN FOR THE COMMODORE 64)
60 ::::::::::::::::::::::::::::::::::::
70 REM *** MAIN PROGRAM ***
80 DIM Y(79),P(20),F(79),S(20),A$(20)
90 FOR J=1 TO 79:READ Y(J):F(J)=1:NEXT
100 FOR J=1 TO 20:READ P(J):READ S(J):NEXT
110 FOR J=1 TO 20:READ A$(J):NEXT
120 GOSUB 200:REM PRINT INSTRUCTIONS
130 X=3
135 K=S(X)+INT(P(X)*RND(1))
136 TF=0:FORI=S(X)TOS(X)+P(X)-1
137 TF=TF+F(I):NEXT
140 IF (RND(1)*TF)
145 GOTO 135
150 TI$="000000":R$=""
152 GET R$:IF R$="" AND TI$<"000005" THEN 152
153 IF R$=CHR$(133) THEN 700
155 IF R$<>"" THEN F(K)=F(K)*1.1:X=Y(K):GOTO135
160 F(K)=.95*F(K):X=Y(K):GOTO135
199 :::::::::::::::::::::::::::::::::::
200 REM *** PRINT INSTRUCTIONS ***
210 PRINTCHR$(147);TAB(12);"REINFORCEMENT":PRINT:PRINT
220 PRINT"     THIS PROGRAM SIMULATES A SKINNER "
230 PRINT"BOX. THE DESCRIPTION OF THE BEHAVIOR OF"
240 PRINT"THE RAT WILL BE DISPLAYED ON THE"
250 PRINT"SCREEN. BY PRESSING ANY KEY, YOU CAN"
260 PRINT"POSITIVELY REINFORCE THAT BEHAVIOR."
270 PRINT"POSITIVELY REINFORCED BEHAVIOR WILL "
271 PRINT"INCREASE IN FREQUENCY.":PRINT
272 PRINT"     PRESS F1 TO STOP OR SAVE BEHAVIOR"
273 PRINT"ON CASSETTE TAPE.":PRINT
281 INPUT"DO YOU WANT TO ENTER BEHAVIOR (Y/N)";BA$
282 IF LEFT$(BA$+"Y",1)<>"Y" THEN 286
283 OPEN 1,1,0,"BEHAVIOR":FOR I=1 TO 79
284 INPUT#1,F(I):IF ST=0 THEN NEXT
285 CLOSE 1:F2=1
286 PRINT"PRESS THE RETURN KEY TO CONTINUE. OK";:INPUT B$
287 PRINTCHR$(147)
290 PRINT"     THE RAT HAS JUST BEEN PLACED IN"
300 PRINT"THE SKINNER BOX.":PRINT:RETURN
310 :::::::::::::::::::::::::::::::::::
400 REM *** DATA ***
410 DATA 2,1,3,5,6,7,8,1,4,5,6,7,8,1,4,5,6,7,8,3,5
420 DATA 1,3,5,6,7,8,13,3,5,6,7,8,17,3,9,10,3,9,10
430 DATA 11,12,11,12,3,9,10,14,15,16,1,3,5,6,7,8,13
440 DATA 14,15,16,14,15,16,18,19,20,1,3,5,6,7,8,17
450 DATA 18,19,20,18,19,20,1,1,6,2,6,8,6,14,2,20,7
460 DATA 22,6,29,3,35,3,39,2,41,2,43,3,45,3,48,7,51
470 DATA 3,58,3,61,3,64,7,67,3,74,3,77
480 DATA RAT APPROACHES PELLET DISPENSER.
490 DATA RAT TURNS AWAY FROM DISPENSER.
500 DATA RAT STANDS ON ALL FOUR LEGS.
510 DATA RAT CONTINUES STANDING ON ALL FOURS.
520 DATA THE RAT JUMPS.
530 DATA RAT WALKS AROUND ON RIGHT SIDE OF BOX.
540 DATA RAT WALKS AROUND ON LEFT SIDE OF BOX.
550 DATA THE RAT LIES DOWN.
560 DATA IT CONTINUES LYING DOWN.
570 DATA IT GOES TO SLEEP.
580 DATA RAT CONTINUES SLEEPING.
590 DATA THE RAT WAKES UP.
600 DATA THE RAT FACES THE BAR PRESS ON HIND LEGS.
610 DATA RAT TURNS AWAY FROM BAR PRESS.
620 DATA NOW THE RAT IS ONLY TOUCHING THE BAR.
630 DATA THE RAT IS PRESSING THE BAR PRESS.
640 DATA RAT FACES PUSH ROD ON HIND LEGS.
650 DATA IT TURNS AWAY FROM PUSH ROD.
660 DATA RAT IS ONLY TOUCHING THE PUSH ROD.
670 DATA THE RAT IS PUSHING THE ROD.
680 :::::::::::::::::::::::::::::::::::
700 REM *** SAVE BEHAVIOR ON TAPE? ***
710 PRINTCHR$(147)
720 INPUT"WANT TO SAVE BEHAVIOR ON TAPE (Y/N)";BC$
730 IF LEFT$(BC$+"Y",1)<>"Y" THEN 760
740 OPEN 1,1,1,"BEHAVIOR":FOR I=1 TO 79
750 PRINT#1,F(I):NEXT:CLOSE 1
760 PRINT:INPUT"WANT TO STOP (Y/N)";BD$
770 IF LEFT$(BD$+"Y")<>"Y" THEN 135
790 :::::::::::::::::::::::::::::::::::