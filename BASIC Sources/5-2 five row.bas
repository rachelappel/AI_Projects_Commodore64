10 :::::::::::::::::::::::::::::::::::
20 REM A FIVE-IN-A-ROW GAME
30 REM WRITTEN BY TIMOTHY J. O'MALLEY
40 REM COPYRIGHT 1983, TAB BOOKS INC.
50 REM (WRITTEN FOR THE COMMODORE 64)
60 ::::::::::::::::::::::::::::::::::
70 PRINT CHR$(147);
75 PRINT "      *** FIVE IN A ROW GAME ***"
80 PRINT:PRINT
81 PRINT "      THE GOAL OF THIS GAME IS TO PLACE"
82 PRINT "FIVE DOTS IN A ROW, EITHER VERTICALLY,"
83 PRINT "HORIZONTALLY OR DIAGONALLY. YOU MAY PUT"
84 PRINT "YOUR DOT ON ANY UNOCCUPIED POSITION."
90 PRINT "ENTER MOVES AS ROW COLUMN. FOR EXAMPLE:"
93 PRINT "H 21 WILL PLACE YOUR DOT AT ROW H AND  "
94 PRINT "COLUMN 21. IF THAT'S THE INTENDED SPOT,"
95 PRINT "PRESS THE RETURN KEY. IF NOT, PRESS ANY"
96 PRINT "OTHER KEY AND RE-ENTER. THE POSITION   "
97 PRINT "WILL FLASH THE DOT BEFORE YOU PRESS THE"
98 PRINT "RETURN KEY. THE CORNERS DISPLAY THE"
99 PRINT "CURRENT COLOR.":PRINT
100 DIM B%(22,38):REM DIMENSION ARRAY FOR BOARD
105 DIM A%(5,6),SR(5,2):REM DIMENSION ARRAYS FOR MOVES
106 DIM CR(5):FOR I=1 TO 5:READ CR(I):NEXT:REM COLORS OF DOTS
107 DATA 7, 1, 8, 13, 3
109 PRINT "THE COMPUTER WILL MOVE LAST."
110 PRINT:INPUT"WHAT NUMBER OF HUMAN PLAYERS (1-4)";NP
120 IF NP<1 OR NP>4 THEN 110
130 PRINT CHR$(147);:REM CLEAR SCREEN
135 F3=0:REM FLAG FOR START OF GAME
140 FOR I=1024 TO 2023:POKE I,81:NEXT
150 FOR I=0 TO 21:POKE(1104+40*I),(1+I):POKE(1143+40*I),(1+I)
160 POKE (55336+40*I),1:POKE(55375+40*I),1:NEXT
170 FOR I=1 TO 38
193 IF I/10>=1 THEN POKE 1024+I,INT(I/10)+48:POKE 55296+I,1
194 IF I/10>=1 THEN POKE 1944+I,INT(I/10)+48:POKE 56216+I,1
200 A=I-10*INT(I/10):A=48+A
210 POKE 1064+I,A:POKE 1984+I,A:POKE 55336+I,1:POKE 56256+I,1:NEXT
215 NC=1
220 IF NC>NP+1 THEN NC=1
230 POKE 55335,CR(NC):POKE 56295,CR(NC)
240 POKE 56256,CR(NC)
245 PRINT CHR$(19);:POKE 55296,CR(NC)
246 FOR I=1024 TO 1031:POKE I,32:NEXT
250 OPEN 1,0
260 INPUT#1,A$:PRINT
270 CLOSE 1:Z=ASC(LEFT$(A$,1))-64:IF Z<1 OR Z>22 THEN 245
280 Y=VAL(MID$(A$+" ",3,2)):IF Y<1 OR Y>38 THEN 245
290 IF B%(Z,Y)>0 THEN 245
300 POKE 1064+40*Z+Y,81:POKE 55336+40*Z+Y,CR(NC)
310 GET C$:POKE 1064+40*Z+Y,32:POKE 55336+40*Z+Y,CR(NC):IF C$="" THEN 300
320 IF CHR$(13)=LEFT$(C$,1) THEN GOSUB 1220:GOTO 340
330 GOTO 245
333 :::::::::::::::::::::::::::::::::::
335 REM *** SEARCH FOR A WINNER ***
340 B%(Z,Y)=NC:V=0:H=0:L=0:K=0
350 FOR I=(Y-5)*-(Y>4)+1 TO (Y-34)*-(Y<35)+38
360 FOR J=(Z-5)*-(Z>4)+1 TO (Z-16)*-(Z<17)+20
370 IF B%(J,I)=NC AND I=Y THEN V=V+1:GOTO 390
375 IF V=5 THEN END
380 IF I=Y AND B%(J,I)<>NC THEN V=0
390 IF B%(J,I)=NC AND J=Z THEN H=H+1:GOTO 410
395 IF H=5 THEN END
400 IF J=Z AND B%(J,I)<>NC THEN H=0
410 IF B%(J,I)=NC AND (Y-I)=(Z-J) THEN L=L+1:GOTO 430
415 IF L=5 THEN END
420 IF (Y-I)=(Z-J) AND B%(J,I)<>NC THEN L=0
430 IF B%(J,I)=NC AND (Y-I)=(J-Z) THEN K=K+1:GOTO 450
435 IF K=5 THEN END
440 IF (Y-I)=(J-Z) AND B%(J,I)<>NC THEN K=0
450 IF H=5 OR V=5 OR L=5 OR K=5 THEN PRINT CHR$(19);:END
460 NEXT:NEXT:A%(NC,1)=Y:A%(NC,2)=Z
470 NC=NC+1:IF NC=NP+1 THEN 495:REM COMPUTER'S MOVE
480 GOTO 220
485 :::::::::::::::::::::::::::::::::::
490 REM *** COMPUTER'S MOVE ***
495 POKE 55335,CR(NC):POKE 56295,CR(NC):POKE 56256,CR(NC)
496 PRINTCHR$(19);"COMPUTER";
500 IF F3=0 THEN 1100:REM START OF GAME - FIND EMPTY SQUARES
505 FY=0:FX=0
510 FOR NX=1 TO NP
520 Y=A%(NX,1):Z=A%(NX,2)
540 V=0:H=0:L=0:K=0:SM=2:FX=0:FY=0
550 FOR I=(Y-5)*-(Y>4)+1 TO (Y-34)*-(Y<35)+38
560 FOR J=(Z-5)*-(Z>4)+1 TO (Z-16)*-(Z<17)+20
570 IF B%(J,I)=NX AND I=Y THEN V=V+1:GOTO 590
580 IF I=Y AND B%(J,I)<>0 AND V>0 THEN V=0:GOTO 590
585 IF B%(J,I)=0 AND I=Y AND V>SM THEN FX=J:FY=I:SM=V
590 IF B%(J,I)=NX AND J=Z THEN H=H+1:GOTO 610
600 IF J=Z AND B%(J,I)<>0 AND H>0 THEN H=0:GOTO 610
605 IF B%(J,I)=0 AND J=Z AND H>SM THEN FX=J:FY=I:SM=H
610 IF B%(J,I)=NX AND (Y-I)=(Z-J) THEN L=L+1:GOTO 630
620 IF (Y-I)=(Z-J) AND B%(J,I)<>0 AND L>0 THEN L=0:GOTO 630
625 IF B%(J,I)=0 AND (Y-I)=(Z-J) AND L>SM THEN FX=J:FY=I:SM=L
630 IF B%(J,I)=NX AND (Y-I)=(J-Z) THEN K=K+1:GOTO 650
640 IF (Y-I)=(Z-J) AND B%(J,I)<>0 AND K>0 THEN K=0:GOTO 650
645 IF B%(J,I)=0 AND (Y-I)=(J-Z) AND K>SM THEN FX=J:FY=I:SM=K
650 NEXT:NEXT
660 A%(NX,3)=V:A%(NX,4)=H
670 A%(NX,5)=L:A%(NX,6)=K
680 NEXT
690 FOR I=1 TO NP:FOR J=3 TO 6
700 IF FX>0 AND FY>0 AND A%(I,J)=SM THEN 1200
710 NEXT:NEXT
805 FY=0:FX=0
810 FOR NX=1 TO NP
820 Y=A%(NX,1):Z=A%(NX,2)
840 V=0:H=0:L=0:K=0:SM=2:FX=0:FY=0
850 FOR I=(Y-34)*-(Y<35)+38 TO (Y-5)*-(Y>4)+1 STEP -1
860 FOR J=(Z-16)*-(Z<17)+20 TO (Z-5)*-(Z>4)+1 STEP -1
870 IF B%(J,I)=NX AND I=Y THEN V=V+1:GOTO 890
880 IF I=Y AND B%(J,I)<>0 AND V>O THEN V=0:GOTO 890
885 IF B%(J,I)=0 AND I=Y AND V>SM THEN FX=J:FY=I:SM=V
890 IF B%(J,I)=NX AND J=Z THEN H=H+1:GOTO 910
900 IF J=Z AND B%(J,I)<>0 AND H>0 THEN H=0:GOTO 910
905 IF B%(J,I)=0 AND J=Z AND H>SM THEN FX=J:FY=I:SM=H
910 IF B%(J,I)=NX AND (Y-I)=(Z-J) THEN L=L+1:GOTO 930
920 IF (Y-I)=(Z-J) AND B%(J,I)<>0 AND L>0 THEN L=0:GOTO 930
925 IF B%(J,I)=0 AND (Y-I)=(Z-J) AND L>SM THEN FX=J:FY=I:SM=L
930 IF B%(J,I)=NX AND (Y-I)=(J-Z) THEN K=K+1:GOTO 950
940 IF (Y-I)=(J-Z) AND B%(J,I)<>0 AND K>0 THEN K=0:GOTO 950
945 IF B%(J,I)=0 AND (Y-I)=(J-Z) AND K>SM THEN FX=J:FY=I:SM=K
950 NEXT:NEXT
960 A%(NX,3)=V:A%(NX,4)=H
970 A%(NX,5)=L:A%(NX,6)=K
980 NEXT
990 FOR I=1 TO NP:FOR J=3 TO 6
1000 IF FX>0 AND FY>0 AND A%(I,J)=SM THEN 1200
1010 NEXT:NEXT
1020 GOTO 1140
1030 ::::::::::::::::::::::::::::::::::
1100 Z1=7+INT(8*RND(1)):Y1=7+INT(24*RND(1)):F3=1
1110 Z2=INT(3*RND(1))-1:Y2=INT(3*RND(1))-1
1115 IF Z2=0 AND Y2=0 AND CT>1 THEN 1140
1117 IF Z2=0 AND Y2=0 THEN 1100
1120 FOR I=1 TO 5:Z3=Z1+Z2*(I-1):Y3=Y1+Y2*(I-1)
1125 IF B%(Z3,Y3)>0 THEN IF B%(Z3,Y3)<>NC THEN 1110
1130 SR(I,1)=Z3:SR(I,2)=Y3:NEXT:CT=1
1140 IF B%(SR(CT,1),SR(CT,2))=0 THEN B%(SR(CT,1),SR(CT,2))=NC:CT=CT+1:GOTO 1190
1150 CT=CT+(CT>1):Z1=SR(CT,1):Y1=SR(CT,2):GOTO 1110
1160 ::::::::::::::::::::::::::::::::::
1190 Z=SR(CT-1,1):Y=SR(CT-1,2):GOSUB 1220:GOTO 340
1200 Z=FX:Y=FY:GOSUB 1220:GOTO 340
1210 ::::::::::::::::::::::::::::::::::
1220 POKE 1064+40*Z+Y,81:POKE 55336+40*Z+Y,CR(NC):RETURN
1230 ::::::::::::::::::::::::::::::::::