10 ::::::::::::::::::::::::::::::::::
20 REM PERFECT LOGIC - PROGRAM 11
30 REM WRITTEN BY TIMOTHY J. O'MALLEY
40 REM COPYRIGHT 1983, TAB BOOKS INC.
50 REM (WRITTEN FOR THE COMMODORE 64)
60 ::::::::::::::::::::::::::::::::::
100 DIM SS$(100),NM$(100),AJ$(100),LK%(100,100):ID=1
110 FORI=1TO100:SS$(I)="S":LK%(I,I)=3:NEXTI:PRINTCHR$(147);
115 PRINTTAB(12);"PERFECT LOGIC":PRINT
116 WH$="N":INPUT "DO YOU WANT TO LOAD FROM TAPE (Y/N)";WH$
117 IF WH$="Y" THEN GOSUB 21000
120 PRINT"{LIGHTBLUE}";:OPEN1,0:INPUT#1,IQ$:PRINT:CLOSE1:PRINT"{CYAN}";
125 IF IQ$="STOP"ORIQ$="END"THENGOSUB 22000:PRINT"{LIGHTBLUE}":END
130 IP$=LEFT$(IQ$,LEN(IQ$)-1)
140 IFLEFT$(IP$,9)="WHAT ARE "THENGOSUB1000:GOTO1200
145 IFLEFT$(IP$,8)="ARE ALL "THENGOSUB19000:GOTO1200
150 IFLEFT$(IP$,8)="WHAT IS "THENGOSUB2000:GOTO1200
155 IFLEFT$(IP$,7)="WHAT'S "THENGOSUB2500:GOTO1200
160 IFLEFT$(IP$,9)="ARE SOME "THENGOSUB3000:GOTO1200
165 IFLEFT$(IP$,13)="ARE NOT SOME "THENGOSUB3500:GOTO1200
170 IFLEFT$(IP$,4)="ARE "THENGOSUB4000:GOTO1200
173 IF LEFT$(IP$,12)="AREN'T SOME "THENGOSUB4250:GOTO1200
175 IF LEFT$(IP$,7)="AREN'T "THENGOSUB4500:GOTO1200
180 IFLEFT$(IP$,3)="IS "THENGOSUB5000:GOTO1200
185 IF LEFT$(IP$,6)="ISN'T "THENGOSUB5500:GOTO1200
190 IFLEFT$(IP$,7)="IS NOT "THENGOSUB6000:GOTO1200
195 IFLEFT$(IP$,4)="ALL "THENGOSUB19500:GOTO1200
200 IFLEFT$(IP$,5)="SOME "THENGOSUB7000
210 FORI=1TOLEN(IP$):IFMID$(IP$,I,8)=" IS NOT "THENGOSUB8000:GOTO1200
220 IFMID$(IP$,I,7)=" ISN'T "THENGOSUB9000:GOTO1200
230 IFMID$(IP$,I,4)=" IS "THENGOSUB10000:GOTO1200
240 IFMID$(IP$,I,9)=" ARE NOT "THENGOSUB11000:GOTO1200
250 IFMID$(IP$,I,8)=" AREN'T "THENGOSUB12000:GOTO1200
260 IFMID$(IP$,I,5)=" ARE "THENGOSUB13000:GOTO1200
270 NEXTI:GOSUB14000:GOTO1200
999 ::::::::::::::::::::::::::::::::::
1000 REM UNDERSTAND "WHAT ARE "
1010 LT$=MID$(IP$,10):IFRIGHT$(LT$,1)="S"THENLT$=LEFT$(LT$,LEN(LT$)-1):F1=1
1020 FORI=IDTO1STEP-1:IFNM$(I)=LT$THENJ1=I:GOTO1040
1030 NEXTI:GOSUB18000:RETURN
1040 FORI=IDTO1STEP-1:IFLK%(J1,I)=3ANDLK%(I,J1)=3THENPRINT"ALL ";
1045 IFLK%(J1,I)=3THENPRINTNM$(J1)SS$(J1)" ARE "NM$(I)SS$(I)"."
1050 IFLK%(J1,I)=2THENPRINT"SOME "NM$(J1)SS$(J1)" ARE "NM$(I)SS$(I)"."
1060 IFLK%(J1,I)=1THENPRINT"NO "NM$(J1)SS$(J1)" ARE "NM$(I)SS$(I)"."
1070 NEXTI:RETURN
1199 ::::::::::::::::::::::::::::::::::
1200 REM COMPUTER DRAWS CONCLUSIONS
1210 FORI1=1TOID-1:FORI2=1TOID-1:IFLK%(I1,I2)=0ORLK%(I2,I1)=0THEN1250
1215 FORI3=1TOID-1:IFLK%(I2,I3)=0ORLK%(I3,I2)=0THEN1240
1217 IFLK%(I1,I3)>0ANDLK%(I3,I1)>0THEN1240
1220 IFLK%(I1,I2)=3ANDLK%(I2,I1)=3THENGOSUB1300
1225 IFLK%(I1,I2)=3ANDLK%(I2,I1)=2THENGOSUB1400
1230 REM IFLK%(I1,I2)=2ANDLK%(I2,I1)=2THENGOSUB1500
1235 REM IFLK%(I1,I2)=1ANDLK%(I2,I1)=1THENGOSUB1600
1240 NEXTI3
1250 NEXTI2,I1:GOTO120
1300 IFLK%(I2,I3)=3ANDLK%(I3,I2)=3THENLK%(I1,I3)=3:LK%(I3,I1)=3:RETURN
1310 IFLK%(I2,I3)=3ANDLK%(I3,I2)=2THENLK%(I1,I3)=3:LK%(I3,I1)=2:RETURN
1320 IFLK%(I2,I3)=2ANDLK%(I3,I2)=2THENLK%(I1,I3)=2:LK%(I3,I1)=2:RETURN
1330 IFLK%(I2,I3)=1ANDLK%(I3,I2)=1THENLK%(I1,I3)=1:LK%(I3,I1)=1:RETURN
1340 RETURN
1400 REM IFLK%(I2,I3)=3ANDLK%(I3,I2)=3THENLK%(I1,I3)=3:LK%(I3,I1)=2:RETURN
1410 IFLK%(I2,I3)=3ANDLK%(I3,I2)=2THENLK%(I1,I3)=3:LK%(I3,I1)=2:RETURN
1420 IFLK%(I2,I3)=1ANDLK%(I3,I2)=1THENLK%(I1,I3)=1:LK%(I3,I1)=1:RETURN
1430 RETURN
1500 REM IFLK%(I2,I3)=3ANDLK%(I3,I2)=3THENLK%(I1,I3)=2:LK%(I3,I1)=2:RETURN
1510 REM IFLK%(I2,I3)=3ANDLK%(I3,I2)=2THENLK%(I3,I1)=2:RETURN
1520 REM RETURN
1600 REM IFLK%(I2,I3)=3ANDLK%(I3,I2)=3THENLK%(I1,I3)=1:LK%(I3,I1)=1:RETURN
1610 REM RETURN
1999 :::::::::::::::::::::::::::::::::
2000 REM UNDERSTAND "WHAT IS{$a0}"
2010 LT$=MID$(IP$,9):IX$=LT$:GOSUB16000:LT$=IX$
2020 FORI=IDTO1STEP-1:IFNM$(I)=LT$THENJ1=I:GOTO1040
2030 NEXTI:GOSUB14000:RETURN
2499 :::::::::::::::::::::::::::::::::
2500 REM UNDERSTAND "WHAT'S "
2510 IP$=" "+IP$:GOTO2010
2599 ::::::::::::::::::::::::::::::::::
3000 REM ANSWER "ARE SOME " QUESTION
3010 IP$=MID$(IP$,10):GOTO4020
3499 :::::::::::::::::::::::::::::::::
3500 REM "ARE NOT SOME " QUESTION
3510 IP$=MID$(IP$,14):GOTO4020
3999 :::::::::::::::::::::::::::::::::
4000 REM ANSWER "ARE " QUESTION
4010 IP$=MID$(IP$,5)
4020 FORM=1TOLEN(IP$):IFMID$(IP$,M,1)=" "THEN4040
4030 NEXTM:GOTO14000
4040 LT$=LEFT$(IP$,M-1)
4050 IFRIGHT$(LT$,1)="S"THENLT$=LEFT$(LT$,LEN(LT$)-1)
4060 FORJ=IDTO1STEP-1:IFLT$=NM$(J)THEN4080
4070 NEXTJ:GOTO4030
4080 RT$=MID$(IP$,M+1):IX$=RT$:GOSUB16000:RT$=IX$:A2$=AR$
4085 IFRIGHT$(RT$,1)="S"THENRT$=LEFT$(RT$,LEN(RT$)-1)
4090 FORK=IDTO1STEP-1:IFRT$=NM$(K)THEN4110
4100 NEXTK:GOTO4070
4110 IFLK%(J,K)=0THENPRINT"I'M NOT SURE.":RETURN
4120 IFLK%(J,K)=1THENPRINT"NONE ARE.":RETURN
4130 IFLK%(J,K)=2THENPRINT"SOME ARE.":RETURN
4140 IFLK%(J,K)=3THENPRINT"YES. ALL ARE.":RETURN
4249 :::::::::::::::::::::::::::::::::
4250 REM "AREN'T SOME " QUESTION
4260 IP$=MID$(IP$,13):GOTO4020
4500 REM "AREN'T " QUESTION
4510 IP$=MID$(IP$,8):GOTO4020
4999 :::::::::::::::::::::::::::::::::
5000 REM "IS " QUESTION
5010 IP$=MID$(IP$,4)
5020 FORM=1TOLEN(IP$):IFMID$(IP$,M,1)=" "THEN5040
5030 NEXTM:GOTO14000
5040 LT$=LEFT$(IP$,M-1)
5050 IX$=LT$:GOSUB16000:LT$=IX$:A1$=AR$
5060 FORJ=IDTO1STEP-1:IFLT$=NM$(J)THEN5080
5070 NEXTJ:GOTO5030
5080 RT$=MID$(IP$,M+1):IX$=RT$:GOSUB16000:RT$=IX$:A2$=AR$
5090 FORK=IDTO1STEP-1:IFRT$=NM$(K)THEN5110
5100 NEXTK:GOTO5070
5110 GOTO4110
5499 :::::::::::::::::::::::::::::::::
5500 REM "ISN'T " QUESTION
5510 IP$=MID$(IP$,7):GOTO5020
5599 :::::::::::::::::::::::::::::::::
6000 REM "IS NOT " QUESTION
6010 IP$=MID$(IP$,8):GOTO5020
6999 :::::::::::::::::::::::::::::::::
7000 REM "SOME " STATEMENT
7010 IP$=MID$(IP$,6):FS=1:RETURN
7999 ::::::::::::::::::::::::::::::::::
8000 REM UNDERSTAND " IS NOT "
8010 LT$=LEFT$(IP$,I-1):RT$=MID$(IP$,I+8)
8020 IF RIGHT$(IQ$,1)="?"THENGOSUB15000:RETURN
8030 GOTO 12020
8999 :::::::::::::::::::::::::::::::::
9000 REM UNDERSTAND " ISN'T "
9010 LT$=LEFT$(IP$,I-1):RT$=MID$(IP$,I+7)
9020 IF RIGHT$(IQ$,1)="?"THENGOSUB15000:RETURN
9030 GOTO 12020
9999 :::::::::::::::::::::::::::::::::
10000 REM UNDERSTAND IS SENTENCE
10010 LT$=LEFT$(IP$,I-1):RT$=MID$(IP$,I+4)
10030 IX$=LT$:GOSUB16000:LT$=IX$:A1$=AR$
10040 IX$=RT$:GOSUB16000:RT$=IX$:A2$=AR$
10045 IF RIGHT$(IQ$,1)="?"THENGOSUB15000:RETURN
10050 FORI=IDTO1STEP-1:IFLT$=NM$(I)THENJ1=I:GOTO10070
10060 NEXTI:NM$(ID)=LT$:AJ$(ID)=A1$:J1=ID:ID=ID+1
10070 FORI=IDTO1STEP-1:IFRT$=NM$(I)THENK1=I:GOTO10085
10080 NEXTI:NM$(ID)=RT$:AJ$(ID)=A2$:K1=ID:ID=ID+1
10085 IF LK%(J1,K1)=3 THEN ON 1+3*RND(1) GOTO 10087,10088,10089
10086 GOTO 10090
10087 PRINT"I KNEW THAT ALREADY.":RETURN
10088 PRINT"I HEARD THAT BEFORE.":RETURN
10089 PRINT"I KNOW.":RETURN
10090 LK%(J1,K1)=3
10095 IFFSTHENFS=0:LK%(J1,K1)=2
10097 IF LK%(K1,J1)>0 THEN GOSUB 17000:RETURN
10100 PRINT"ARE ALL "NM$(K1)SS$(K1)" "NM$(J1)SS$(J1)"?"
10110 PRINT"{LIGHTBLUE}";:OPEN1,0:INPUT#1,AN$:PRINT:CLOSE1:PRINT"{CYAN}";
10120 IFLEFT$(AN$,1)="Y"THENLK%(K1,J1)=3:GOSUB17000:RETURN
10125 IF LEFT$(AN$,1)="N" THEN LK%(K1,J1)=2:GOSUB 17000:RETURN
10130 PRINT"SOME "NM$(K1)SS$(K1)" ARE "NM$(J1)SS$(J1)". ":GOSUB17000:RETURN
10999 ::::::::::::::::::::::::::::::::
11000 REM UNDERSTAND " ARE NOT "
11010 LT$=LEFT$(IP$,I-1):RT$=MID$(IP$,I+9):GOTO12020
11999 ::::::::::::::::::::::::::::::
12000 REM UNDERSTAND " AREN'T "
12010 LT$=LEFT$(IP$,I-1):RT$=MID$(IP$,I+8)
12020 IX$=LT$:GOSUB16000:LT$=IX$:A1$=AR$
12030 IX$=RT$:GOSUB16000:RT$=IX$:A2$=AR$
12040 IFRIGHT$(LT$,1)="S"THENLT$=LEFT$(LT$,LEN(LT$)-1):F1=1
12050 IFRIGHT$(RT$,1)="S"THENRT$=LEFT$(RT$,LEN(RT$)-1):F2=1
12060 FORI=IDTO1STEP-1:IFLT$=NM$(I)THENJ1=I:GOTO12080
12070 NEXTI:NM$(ID)=LT$:AJ$(ID)=A1$:J1=ID:ID=ID+1
12080 FORI=IDTO1STEP-1:IFRT$=NM$(I)THENK1=I:GOTO12100
12090 NEXTI:NM$(ID)=RT$:AJ$(ID)=A2$:K1=ID:ID=ID+1
12100 IFF1THENSS$(J1)="S":F1=0
12110 IFF2THENSS$(K1)="S":F2=0
12120 GOTO20000
12999 :::::::::::::::::::::::::::::::
13000 REM UNDERSTAND " ARE "
13010 LT$=LEFT$(IP$,I-1):RT$=MID$(IP$,I+5)
13020 IX$=LT$:GOSUB16000:LT$=IX$:A1$=AR$
13030 IX$=RT$:GOSUB16000:RT$=IX$:A2$=AR$
13040 IFRIGHT$(LT$,1)="S"THENLT$=LEFT$(LT$,LEN(LT$)-1):F1=1
13050 IFRIGHT$(RT$,1)="S"THENRT$=LEFT$(RT$,LEN(RT$)-1):F2=1
13060 FORI=IDTO1STEP-1:IFLT$=NM$(I)THENJ1=I:GOTO13080
13070 NEXTI:NM$(ID)=LT$:AJ$(ID)=A1$:J1=ID:ID=ID+1
13080 FORI=IDTO1STEP-1:IFRT$=NM$(I)THENK1=I:GOTO13100
13090 NEXTI:NM$(ID)=RT$:AJ$(ID)=A2$:K1=ID:ID=ID+1
13100 SS$(J1)="":IFF1THENSS$(J1)="S":F1=0
13110 SS$(K1)="":IFF2THENSS$(K1)="S":F2=0
13120 GOTO10085
13999 :::::::::::::::::::::::::::::::
14000 REM DO NOT UNDERSTAND INPUT
14005 PRINTLT$;"? ";
14010 ON1+INT(8*RND(1))GOTO14020,14030,14040,14050,14060,14070,14080,14090
14020 PRINT"I DON'T DIG.":RETURN
14030 PRINT"WHAT?":RETURN
14040 PRINT"WHAT ARE YOU SAYING?":RETURN
14050 PRINT"HUH?":RETURN
14060 PRINT"I DON'T FOLLOW.":RETURN
14070 PRINT"I DON'T UNDERSTAND.":RETURN
14080 PRINT"YOU ARE NOT MAKING SENSE.":RETURN
14090 PRINT"IF YOU SAY SO, BUT I DON'T KNOW.":RETURN
14999 :::::::::::::::::::::::::::::::
15000 REM STATEMENT IS REALLY QUESTION
15010 FORI=IDTO1STEP-1:IFLT$=NM$(I)THEN15030
15020 NEXTI:GOTO14000
15030 FORK=IDTO1STEP-1:IFRT$=NM$(K)THEN15050
15040 NEXTK:GOTO14000
15050 GOTO5110
15999 :::::::::::::::::::::::::::::::
16000 REM FIND ARTICLES OF IX$
16010 DATA " THE "," A "," AN ","THE ","A ","AN "
16020 AR$="":FORI=1TO6:READAT$:LA=LEN(AT$)
16030 IFMID$(IX$,1,LA)=AT$THENIX$=MID$(IX$,LA+1):AR$=AT$:RESTORE:RETURN
16040 NEXTI:RESTORE:RETURN
16999 ::::::::::::::::::::::::::::::::
17000 REM INPUT UNDERSTOOD
17010 ON1+INT(8*RND(1))GOTO17020,17030,17040,17050,17060,17070,17080,17090
17020 PRINT"I UNDERSTAND NOW.":RETURN
17030 PRINT"OKAY.":RETURN
17040 PRINT"ALL RIGHT.":RETURN
17050 PRINT"I SEE.":RETURN
17060 PRINT"OK. THAT MAKES SENSE.":RETURN
17070 PRINT"I GET IT. WHAT ELSE CAN I LEARN?":RETURN
17080 PRINT"NOW WE ARE GETTING SOMEWHERE.":RETURN
17090 PRINT"GOOD. LET'S GO ON.":RETURN
17999 ::::::::::::::::::::::::::::::::
18000 REM DID NOT FIND NOUN IN QUESTION
18010 ON1+INT(8*RND(1))GOTO18020,18030,18040,18050,18060,18070,18080,18090
18020 PRINT"YOU'RE ASKING ME? I DON'T KNOW ABOUT "LT$;:GOTO18100
18030 PRINT"GOT ME. I HAVE NO INFO ON "LT$;:GOTO18100
18040 PRINT"BEATS ME.":RETURN
18050 PRINT"I WOULD NOT KNOW ABOUT "LT$;:GOTO18100
18060 PRINT"I HAVE NO INFO ABOUT "LT$;:GOTO18100
18070 PRINT"YOUR GUESS IS AS GOOD AS MINE.":RETURN
18080 PRINT"GOOD QUESTION. I DON'T KNOW.":RETURN
18090 PRINT"I HAVE NO IDEA.":RETURN
18100 IF F1THENPRINT"S";:F1=0
18110 PRINT".":RETURN
18999 :::::::::::::::::::::::::::::::
19000 REM "ARE ALL " QUESTION
19010 IP$=MID$(IP$,9):GOTO4020
19499 :::::::::::::::::::::::::::::::::
19500 REM "ALL " STATEMENT
19510 IP$=MID$(IP$,5):RETURN
19999 ::::::::::::::::::::::::::::::::
20000 REM SET NOT CONDITION
20010 LK%(J1,K1)=1:LK%(K1,J1)=1:GOTO17000
20998 :::::::::::::::::::::::::::::::
20999 REM *** LOAD DATA ***
21000 D$="":INPUT"WHAT FILE NAME";D$
21010 OPEN1,1,0,D$
21020 INPUT#1,ID
21030 FOR I=1 TO ID-1:INPUT#1,SS$(I):NEXT I
21040 FOR I=1 TO ID-1:INPUT#1,NM$(I):NEXT I
21050 FOR I=1 TO ID-1:INPUT#1,AJ$(I):NEXT I
21060 FOR I=1 TO ID-1:FORJ=1TOID-1:INPUT#1,LK:LK%(I,J)=LK:NEXT J,I
21070 CLOSE 1:RETURN
21998 :::::::::::::::::::::::::::::::::
21999 REM *** SAVE DATA ***
22000 WH$="N":INPUT"WANT TO SAVE WORK (Y/N)";WH$:IFWH$="N"THEN END
22005 D$="":INPUT "WHAT FILE NAME";D$
22010 OPEN1,1,1,D$
22020 PRINT#1,ID
22030 FOR I=1 TO ID-1:PRINT#1,SS$(I):NEXT I
22040 FOR I=1 TO ID-1:PRINT#1,NM$(I):NEXT I
22050 FOR I=1 TO ID-1:PRINT#1,AJ$(I):NEXT I
22060 FOR I=1 TO ID-1:FORJ=1TOID-1:LK=LK%(I,J):PRINT#1,LK:NEXT J,I
22070 CLOSE 1:RETURN
22999 :::::::::::::::::::::::::::::::::