10 ::::::::::::::::::::::::::::::::::::
15 REM TOWERS OF HANOI
20 REM WRITTEN BY TIMOTHY J. O'MALLEY
25 REM COPYRIGHT 1983, TAB BOOKS INC.
30 REM (WRITTEN FOR THE COMMODORE 64)
35 ::::::::::::::::::::::::::::::::::::
40 REM *** MAIN PROGRAM ***
50 GOSUB 100: REM PRINT INSTRUCTIONS
60 GOSUB 300:REM DEFINES SPRITES
70 GOSUB 500:REM DETERMINE SOLUTION
80 END
90 ::::::::::::::::::::::::::::::::::::
100 REM *** PRINT INSTRUCTIONS ***
105 NM=0
110 PRINTCHR$(147);:REM CLEAR SCREN
120 PRINT"   ARTIFICIAL INTELLIGENCE - PROGRAM 1"
130 PRINT:PRINT"         THE TOWERS OF HANOI":PRINT:PRINT
140 PRINT"     THE PROBLEM IS TO MOVE ALL OF THE"
150 PRINT"DISKS FROM THE FIRST POST TO THE THIRD"
160 PRINT"POST, ONE DISK AT A TIME, WITHOUT "
170 PRINT"PLACING A LARGER DISK ON TOP OF A "
180 PRINT"SMALLER DISK.":PRINT
190 PRINT"     YOU MAY SPECIFY 1-8 DISKS.":PRINT
200 OPEN1,0
210 PRINT"ENTER NUMBER OF DISKS: ";:INPUT#1,N$
220 PRINT:CLOSE1
230 IF LEN(N$)>1THENPRINT:GOTO190
240 N=VAL(N$):IFN>8ORN<1THENPRINT:GOTO190
250 PRINT:PRINT"VERY WELL."
260 RETURN
270 :::::::::::::::::::::::::::::::::::
300 REM *** DEFINE SPRITES ***
305 POKE53281,15:REM BORDER GRAY3
310 V=53248:POKEV+32,15:IF N=8 THEN 320
315 FORS=NTO8:POKEV-2+2*S,0:POKEV-1+2*S,0:NEXTS
320 FORS=1TON:POKEV+21,PEEK(V+21)OR(2^(S-1)):REM ENABLE SPRITES
330 POKEV+23,PEEK(V+23)OR(2^(S-1)):REM EXPAND IN Y DIRECTION
340 POKEV+29,PEEK(V+29)OR(2^(S-1)):REM EXPAND IN X DIRECTION
350 POKEV+37+2*S,S+5:REM SET SPRITES' COLORS
360 POKEV-2+2*S,50:REM SET EACH SPRITE TO X COORDINATE AT POST 1
370 POKEV-1+2*S,200:REM SET EACH SPRITE TO Y COORDINATE AT POST 1
375 VL=191+S
380 POKE2039+S,VL:REM SET SPRITE'S POINTERS
385 FORK=64*VLTO64*VL+63:POKEK,0:NEXTK
390 FORK=64*VL+6*(S-1)TO64*VL+6*(S-1)+3STEP3
400 POKEK,2^S-1
410 POKEK+1,255
420 POKEK+2,255-(2^(8-S)-1)
430 NEXTK,S
440 PRINTCHR$(147);:REM CLEAR SCREEN
450 FORK=1TO15:PRINTCHR$(17);:NEXTK
460 FORK=1TO8:PRINTTAB(6);CHR$(98);TAB(18);CHR$(98);
470 PRINTTAB(30);CHR$(98)
480 NEXTK:PRINT:PRINTTAB(6);"1";TAB(18);"2";TAB(30);"3"
485 PRINTCHR$(19):RETURN
490 :::::::::::::::::::::::::::::::::::
500 REM *** DETERMINE SOLUTION ***
510 T(1)=2^N-1
520 NM=NM+1:S=0
530 D=-((NMAND2^S)=2^S)*(S+1):S=S+1:IFD=0THEN530
540 TF=0
550 TF=TF+1:IF0=((2^(D-1)ANDT(TF))=(2^(D-1)))THEN550
560 DT=(((1ANDN)=1)*2+1)*(((1ANDD)=1)*2+1)
570 TT=TF-DT-DT*3*((TF-DT>3)OR(TF-DT<1))
580 T(TF)=T(TF)-2^(D-1)
590 T(TT)=T(TT)+2^(D-1)
600 GOSUB700:REM DISPLAY DISK MOTION
610 IF NM<2^N-1THEN520
620 PRINT"DONE.":RETURN
630 :::::::::::::::::::::::::::::::::::
700 REM *** DISPLAY DISK MOTION ***
710 FORY=200TO140STEP-2:POKEV-1+2*S,Y:NEXTY
720 FORX=(TF-1)*96+52TO(TT-1)*96+52STEPSGN(TT-TF)*2
730 POKEV-2+2*S,X:NEXTX
740 FORY=140TO200STEP2:POKEV-1+2*S,Y:NEXTY
750 RETURN