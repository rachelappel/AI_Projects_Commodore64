5 :::::::::::::::::::::::::::::::::::::
10 REM HEURISTIC TIC TAC TOE - PROG. 12
20 REM WRITTEN BY TIMOTHY J. O'MALLEY
30 REM COPYRIGHT 1984, TAB BOOKS INC.
40 REM (WRITTEN FOR THE COMMODORE 64)
45 ::::::::::::::::::::::::::::::::::::
47 REM *** MAIN PROGRAM ***
50 POKE 51,0:POKE 52,64:POKE 55,0:POKE 56,64
55 PRINT "HANG ON A MINUTE WHILE I DO SOME HOUSE-"
60 PRINT "KEEPING.....THANKS!!"
70 G=0:FOR I=16384 TO 36077:POKE I,0:NEXT
80 G=G+1:T=0
90 PRINT CHR$(147);"     *** HEURISTIC TIC TAC TOE ***":PRINT
100 PRINT "GAME NO. ";G:PRINT
110 FOR J=1 TO 3
120 FOR K=1 TO 3
130 B(J,K)=0
140 L=3*J+K-3
150 R(L)=0
160 C(L)=0:S(L)=0
170 NEXT K,J
180 M=0:REM M IS THE MOVE NUMBER
190 GOSUB 900:GOSUB 4000:REM DETERMINE WHO GOES FIRST & PRINT BOARD
200 M=M+1:REM INCREASE MOVE NUMBER
210 PRINT:PRINT "MOVE NO. ";M:PRINT
220 M2=M/2:M3=M2<>INT(M2)
230 M4=M2=INT(M2)
240 IF (Z=1 AND M3) OR (Z=2 AND M4) THEN GOSUB 2000:REM COMPUTER'S MOVE
250 IF (Q=1 AND M3) OR (Q=2 AND M4) THEN GOSUB 3000:REM YOUR MOVE
260 GOSUB 4000:REM PRINT BOARD
270 GOSUB 1000:REM ANY WINNER YET?
280 IF A=1 THEN 330:REM STORE WINNING STRATEGY
290 IF M<9 THEN 200
300 PRINT:INPUT "NOBODY WON. WANT TO PLAY AGAIN";R1$
310 IF LEFT$(R1$,1)="Y" THEN 80:REM GO TO THE BEGINNING OF THE NEXT GAME
320 END
325 :::::::::::::::::::::::::::::::::::
327 REM *** STORE WINNING STRATEGY ***
330 FOR L=W TO M STEP 2
340 POKE (16384+S(L)),(3*R(L)+C(L)-3)
350 NEXT
360 POKE (16384+S(M-1)),(3*R(M)+C(M)-3)
430 IF W=Z THEN PRINT:PRINT "I WIN!   ":PRINT
440 IF W=Q THEN PRINT:PRINT "YOU WIN! ":PRINT
450 INPUT "WANT TO PLAY ANOTHER GAME";R1$
460 GOTO 310
470 :::::::::::::::::::::::::::::::::::
480 REM *** DETERMINE WHO GOES FIRST ***
900 Z=1
910 Q=2
920 IF RND(1)<0.5 THEN 950
930 PRINT "I WILL BE X AND WILL MOVE FIRST."
940 RETURN
950 Z=2
960 Q=1
970 PRINT "YOU CAN BE X AND CAN MOVE FIRST."
980 RETURN
990 :::::::::::::::::::::::::::::::::::
995 REM *** SEARCH FOR A WINNER ***
1000 A=0
1010 Z3=0
1020 Z4=0
1030 FOR J=1 TO 3
1040 Z2=0
1050 Z1=0
1060 FOR K=1 TO 3
1070 IF B(K,J)=W THEN Z2=Z2+1
1080 IF B(J,K)=W THEN Z1=Z1+1
1090 IF B(K,J)=W AND J=K THEN Z3=Z3+1
1100 IF B(K,J)=W AND J+K=4 THEN Z4=Z4+1
1110 NEXT
1120 IF Z1=3 THEN A=1
1130 IF Z2=3 THEN A=1
1140 NEXT
1150 IF Z3=3 THEN A=1
1160 IF Z4=3 THEN A=1
1170 RETURN
1180 ::::::::::::::::::::::::::::::::::
1190 REM *** COMPUTER'S MOVE ***
2000 PRINT "MY MOVE IS ";
2010 W=Z:Y=Q
2020 R2=PEEK(16384+T):IF R2=0 THEN 2040
2025 IF R2>9 THEN STOP
2030 J=INT(1+(R2-1)/3):K=R2-3*INT((R2-1)/3):R(M)=J:C(M)=K:GOTO 2110
2040 FOR J=1 TO 3
2050 FOR K=1 TO 3
2055 IF R(M)>0 THEN 2100
2060 IF B(J,K)>0 THEN 2100
2070 R(M)=J
2080 R2=3*J+K-3
2090 C(M)=K
2100 NEXT K,J
2110 B(R(M),C(M))=Z:S(M)=T:T=T+Z*3^(3*R(M)+C(M)-4)
2120 PRINT "ROW";STR$(R(M));", COLUMN";STR$(C(M))"."
2130 RETURN
2140 ::::::::::::::::::::::::::::::::::
2150 REM *** YOUR MOVE SUBROUTINE ***
3000 W=Q:Y=Z
3010 PRINT "YOUR MOVE. ";
3020 INPUT "WHAT ROW AND COLUMN";R(M),C(M)
3025 IF R(M)>3 OR R(M)<1 OR C(M)>3 OR C(M)<1 THEN 3060
3030 IF B(R(M),C(M))>0 THEN 3060
3040 B(R(M),C(M))=Q:S(M)=T:T=T+Q*3^(3*R(M)+C(M)-4)
3050 RETURN
3060 PRINT "INVALID MOVE. PLEASE TRY AGAIN."
3070 GOTO 3020
3080 ::::::::::::::::::::::::::::::::::
3090 REM *** PRINT OUT BOARD ***
4000 PRINT:FOR J=1 TO 3
4010 FOR K=1 TO 3
4020 PRINT TAB((K-1)*4+2);
4030 IF B(J,K)=1 THEN PRINT "X";
4040 IF B(J,K)=2 THEN PRINT "O";
4050 IF K<3 THEN PRINT TAB(4*K);"B";
4060 NEXT
4070 PRINT
4080 IF J=3 THEN 4100
4090 PRINT " {$c0}{$c0}{$c0}{$db}{$c0}{$c0}{$c0}{$db}{$c0}{$c0}{$c0}"
4100 NEXT
4120 RETURN
4130 ::::::::::::::::::::::::::::::::::