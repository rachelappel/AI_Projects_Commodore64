10 ::::::::::::::::::::::::::::::::::::
20 REM MAZE - PROGRAM 5
30 REM WRITTEN BY TIMOTHY J. O'MALLEY
40 REM COPYRIGHT 1983, TAB BOOKS INC.
50 REM (WRITTEN FOR THE COMMODORE 64)
60 ::::::::::::::::::::::::::::::::::::
70 REM *** MAIN PROGRAM ***
80 GOSUB 200:REM INSTRUCTIONS
100 GOSUB 600:REM READ MAZE DATA
110 GOSUB 720:REM DETERMINE SOLUTION
120 OPEN 1,0:INPUT#1,AI$:PRINT"{HOME}":END
130 :::::::::::::::::::::::::::::::::::
200 REM *** INSTRUCTIONS ***
205 PRINT"{WHITE}";
207 POKE 53281,4
210 PRINTCHR$(147);TAB(12);"MAZE - PROGRAM 5":PRINT:PRINT
220 PRINT"    THIS PROGRAM ALLOWS THE COMPUTER TO"
230 PRINT"FIND ITS WAY THROUGH A MAZE. THE MAZE "
240 PRINT"IS DEFINED IN THE DATA STATEMENTS IN "
250 PRINT"THE PROGRAM LISTING, LINES 640-710."
260 PRINT:INPUT"PRESS A KEY TO CONTINUE. OK";AI$
390 RETURN
395 :::::::::::::::::::::::::::::::::::
400 REM *** CODES FOR SIDES OF MAZE ***
401 PRINT"{$b7}{$b7}{$b7}{$b7}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}";:RETURN
402 PRINT"{CURSOR RIGHT}{CURSOR RIGHT}{CURSOR RIGHT}{$aa}{CURSOR LEFT}{DOWN}{$aa}{CURSOR LEFT}{DOWN}{$aa}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR UP}{CURSOR UP}";:RETURN
403 PRINT"{CURSOR RIGHT}{CURSOR RIGHT}{CURSOR RIGHT}P{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}";:RETURN
404 PRINT"{DOWN}{DOWN}{$af}{$af}{$af}{$af}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR UP}{CURSOR UP}";:RETURN
406 PRINT"{DOWN}{DOWN}{CURSOR RIGHT}{CURSOR RIGHT}{CURSOR RIGHT}{$ba}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR LEFT}{CURSOR UP}{CURSOR UP}";:RETURN
408 PRINT"{$b4}{CURSOR LEFT}{DOWN}{$b4}{CURSOR LEFT}{DOWN}{$b4}{CURSOR LEFT}{CURSOR UP}{CURSOR UP}";:RETURN
409 PRINT"O{CURSOR LEFT}";:RETURN
412 PRINT"{DOWN}{DOWN}L{CURSOR LEFT}{CURSOR UP}{CURSOR UP}";:RETURN
590 :::::::::::::::::::::::::::::::::::
600 REM *** READ MAZE DATA ***
610 DIM B(8,8)
615 PRINT "{CLR}";
620 FORI=1 TO 8:FOR J=1 TO 8
622 READ B(I,J):IF B(I,J)=0 THEN 639
624 FOR K=0 TO 3:ON ((B(I,J) AND 2^K)=2^K)*-(K+1) GOSUB 401,402,404,408
625 NEXT:FOR K=1 TO 4
630 ON ((B(I,J) AND 3*K)=3*K)*-K GOSUB 403,406,409,412
635 NEXT
639 PRINT"{CURSOR RIGHT}{CURSOR RIGHT}{CURSOR RIGHT}{CURSOR RIGHT}";:NEXT:PRINT:PRINT:PRINT:NEXT
640 DATA 10,9,3,9,5,3,13,3
650 DATA 10,14,10,10,13,4,5,2
660 DATA 12,5,4,6,9,5,3,10
670 DATA 11,9,5,5,6,11,10,10
680 DATA 10,10,13,5,1,6,12,6
690 DATA 10,12,5,5,0,5,5,7
700 DATA 8,5,5,7,10,9,5,3
710 DATA 12,5,5,5,4,4,7,10
715 RETURN
717 :::::::::::::::::::::::::::::::::::
720 PRINT "{HOME}";:F=0:FOR I=1 TO 8:FOR J=1 TO 8
730 IF B(I,J)=7 THEN F=1:B(I,J)=15:B(I,J-1)=B(I,J-1)+2
740 IF B(I,J)=11 THEN F=1:B(I,J)=15:B(I+1,J)=B(I+1,J)+1
750 IFB(I,J)=13 THEN F=1:B(I,J)=15:B(I,J+1)=B(I,J+1)+8
760 IF B(I,J)=14 THEN F=1:B(I,J)=15:B(I-1,J)=B(I-1,J)+4
770 NEXT J,I
780 IF F THEN 720
790 FOR I=1 TO 8:FOR J=1 TO 8
800 IF B(I,J)=15 THEN PRINT "{CURSOR RIGHT}{CURSOR RIGHT}{CURSOR RIGHT}{CURSOR RIGHT}";:GOTO 810
806 PRINT "{DOWN}{CURSOR RIGHT}{$a6} {CURSOR RIGHT}{CURSOR UP}";
810 NEXT:PRINT:PRINT:PRINT:NEXT:RETURN
820 :::::::::::::::::::::::::::::::::::