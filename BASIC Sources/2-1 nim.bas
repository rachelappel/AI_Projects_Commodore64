10 ::::::::::::::::::::::::::::::::::::
15 REM GAME OF NIM - PROGRAM 4
20 REM WRITTEN BY TIMOTHY J. O'MALLEY
25 REM COPYRIGHT 1984, TAB BOOKS INC.
30 REM (WRITTEN FOR THE COMMODORE 64)
35 ::::::::::::::::::::::::::::::::::::
37 REM *** MAIN PROGRAM ***
40 GOSUB 105:REM INITIALIZE PROGRAM
45 IF F=0 THEN GOSUB 710:GOSUB 405:GOSUB 540:IF TL=0 THEN 70
50 GOSUB 710:GOSUB 200:GOSUB 540:IF TL=0 THEN 70
55 IF F=1 THEN GOSUB 710:GOSUB 405:GOSUB 540:IF TL=0 THEN 70
60 GOTO 45
70 INPUT"WANT TO PLAY AGAIN (Y/N)";A$
80 PRINT:IF LEFT$(A$+" ",1)="Y" THEN GOSUB 119:GOTO 45
90 INPUT"WANT TO SAVE STRATEGY (Y/N)";A$
92 PRINT:IF LEFT$(A$+" ",1)="N" THEN END
94 OPEN 1,1,1,"STRATEGY":FOR I=1 TO 512
96 PRINT#1,P%(I):NEXT:CLOSE 1:END
98 ::::::::::::::::::::::::::::::::::::
100 REM *** INITIALIZE PROGRAM ***
105 DIM P%(584),Q%(3,24)
110 GOSUB 810
114 A$="Y":INPUT"ENTER STRATEGY FROM TAPE (Y/N)";A$
115 PRINT:IF LEFT$(A$+" ",1)="N" THEN 119
116 OPEN 1,1,0,"STRATEGY":FOR I=1 TO 584
117 INPUT#1,P%(I):IF ST=0 THEN NEXT
118 CLOSE 1
119 M=1:F=0
120 IF RND(1)<0.5 THEN PRINT "YOU MAY MOVE FIRST.":F=1:GOTO 140
130 PRINT "THE COMPUTER WILL MOVE FIRST."
140 FOR I=1 TO 3
150 Q%(I,1)=3+5*RND(1)
160 FOR J=2 TO 24
170 Q%(I,J)=0
180 NEXT:NEXT
190 GOSUB 460:RETURN
195 :::::::::::::::::::::::::::::::::::
200 REM *** YOUR MOVE ***
210 OPEN 1,0:PRINT "YOUR MOVE. ":PRINT
220 PRINT "ENTER PILE #:";:INPUT#1,PN:PRINT
230 IF PN<1 OR PN>3 THEN PRINT "ILLEGAL PILE #. TRY AGAIN.":PRINT:GOTO 220
235 IF Q%(PN,M)=0 THEN PRINT "NONE IN THAT PILE. TRY AGAIN.":PRINT:GOTO 220
240 PRINT:PRINT "ENTER NUMBER TAKEN FROM PILE #"PN":";:INPUT#1,NB:PRINT
250 IF NB>3 THEN PRINT "TOO MANY. 3 IS MAXIMUM. TRY AGAIN.":PRINT:GOTO 240
260 IF NB<1 THEN PRINT "COME ON NOW. TAKE AT LEAST 1.":PRINT:GOTO 240
270 IF Q%(PN,M)
280 CLOSE 1:PRINT:PRINT "VERY WELL.":PRINT
290 Q%(0,M)=(PN-1)*3+NB
300 FORI=1TO3
310 Q%(I,M+1)=Q%(I,M)+NB*(I=PN)
320 NEXT:M=M+1:RETURN
330 :::::::::::::::::::::::::::::::::::
400 REM *** COMPUTER'S MOVE ***
405 PRINT "COMPUTER'S MOVE.":PRINT
410 C=Q%(1,M)+Q%(2,M)*8+Q%(3,M)*64
420 IF P%(C)=0THEN GOSUB 480:GOTO 450
430 PN=1-(P%(C)>3)-(P%(C)>6)
440 NB=P%(C)-(PN-1)*3
450 PRINT "COMPUTER WILL TAKE"NB"FROM PILE #"PN".":PRINT
455 GOSUB 290
460 TI$="000000"
465 IF TI$<"000004" THEN 465
466 RETURN
470 :::::::::::::::::::::::::::::::::::
475 REM *** RANDOM COMPUTER MOVE ***
480 PN=1+INT(3*RND(1))
490 IFQ%(PN,M)=0THENPN=PN+1:PN=PN+3*(PN>3):GOTO490
500 J=Q%(PN,M):NB=1+INT(-RND(1)*(J>1)-RND(1)*(J>2))
510 RETURN
520 :::::::::::::::::::::::::::::::::::
530 REM *** IS GAME OVER? ***
540 TL=0:FOR I=1 TO 3
550 TL=TL+Q%(I,M)
560 NEXT:IF TL THEN RETURN
570 PRINT CHR$(147);"            GAME OVER!":PRINT
580 IF INT((M-F)/2)=(M-F)/2 THEN PRINT"COMPUTER LOST! YOU WON!":GOTO 600
590 PRINT "YOU LOST! COMPUTER WON!"
600 PRINT:FORI=M-2 TO 1 STEP -2
605 PN=1-(Q%(0,I)>3)-(Q%(0,I)>6)
606 NB=Q%(0,I)-(PN-1)*3
610 P%(Q%(1,I)+Q%(2,I)*8+Q%(3,I)*64)=Q%(0,I)
611 IF PN=1 THEN P%(Q%(1,I)+Q%(3,I)*8+Q%(2,I)*64)=Q%(0,I)
612 IF PN=2 THEN P%(Q%(3,I)+Q%(2,I)*8+Q%(1,I)*64)=Q%(0,I)
613 IF PN=3 THEN P%(Q%(2,I)+Q%(1,I)*8+Q%(3,I)*64)=Q%(0,I)
614 IF PN=1 THEN P%(Q%(2,I)+Q%(1,I)*8+Q%(3,I)*64)=3+NB
615 IF PN=1 THEN P%(Q%(3,I)+Q%(1,I)*8+Q%(2,I)*64)=3+NB
616 IF PN=2 THEN P%(Q%(1,I)+Q%(3,I)*8+Q%(2,I)*64)=6+NB
617 IF PN=2 THEN P%(Q%(3,I)+Q%(1,I)*8+Q%(2,I)*64)=6+NB
618 IF PN=3 THEN P%(Q%(3,I)+Q%(1,I)*8+Q%(2,I)*64)=NB
619 IF PN=3 THEN P%(Q%(3,I)+Q%(2,I)*8+Q%(1,I)*64)=NB
620 NEXT:RETURN
630 :::::::::::::::::::::::::::::::::::
700 REM *** PRINT PILES ***
710 PRINT CHR$(147):FOR I=1 TO 3
720 PRINT "PILE #"I":  ";
730 FORJ=1 TO Q%(I,M)
740 IF J>Q%(I,M) THEN 780
750 PRINT CHR$(111)CHR$(17)CHR$(157);
760 PRINT CHR$(108)CHR$(186)CHR$(145);
770 PRINT CHR$(157)CHR$(112)" ";
780 NEXT:PRINT:PRINT:PRINT:NEXT:RETURN
790 :::::::::::::::::::::::::::::::::::
800 REM *** INSTRUCTIONS ***
810 PRINT CHR$(147);"         *** GAME OF NIM ***":PRINT:PRINT
820 PRINT"     IN THIS GAME YOU PICK FROM 1 TO 3"
830 PRINT"OBJECTS FROM ANY SINGLE PILE. EACH OF"
840 PRINT"THREE PILES WILL CONTAIN FROM 3 TO 8"
850 PRINT"OBJECTS. YOU WIN BY MAKING THE COMPUTER"
860 PRINT"PICK UP THE LAST OBJECT.":PRINT:PRINT
870 RETURN
880 :::::::::::::::::::::::::::::::::::